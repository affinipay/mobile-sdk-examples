apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
  compileSdkVersion 26
  buildToolsVersion '26.0.2'
  defaultConfig {
    applicationId "com.affinipay.cardreader"
    minSdkVersion 14
    targetSdkVersion 26
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    multiDexEnabled true
  }
  buildTypes {
    debug {
      buildConfigField 'String', 'LOCAL_SERVER', "\"http://${getIP()}:9292\""
    }
    release {
      minifyEnabled false
      buildConfigField 'String', 'LOCAL_SERVER', "\"http://${getIP()}:9292\""
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  testOptions {
    unitTests.all {
      setIgnoreFailures(true)
    }
  }
}

kapt {
  generateStubs = true
}

dependencies {

  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation files('libs/AffiniPay-MobileSDK.aar')

  implementation 'com.android.support:appcompat-v7:26.1.0'
  implementation 'com.android.support.constraint:constraint-layout:1.0.2'
  implementation 'com.android.support:design:26.1.0'
  implementation 'com.android.support:multidex:1.0.2'

  implementation "com.google.dagger:dagger:2.11"
  implementation "com.google.dagger:dagger-android:2.11"
  implementation "com.google.dagger:dagger-android-support:2.11"
  kapt "com.google.dagger:dagger-compiler:2.11"
  kapt "com.google.dagger:dagger-android-processor:2.11"
  implementation 'com.jakewharton:butterknife:8.5.1'
  annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
  implementation 'com.squareup.retrofit2:retrofit:2.3.0'
  implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
  implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
  implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
  implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
  implementation 'io.reactivex.rxjava2:rxjava:2.1.0'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

  kaptAndroidTest 'com.google.dagger:dagger-compiler:2.10'

  androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  androidTestImplementation 'org.mockito:mockito-core:2.6.2'
  androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito:2.2.0'

  testImplementation 'junit:junit:4.12'
}
repositories {
  mavenCentral()
}

def getIP() {
  InetAddress result = null
  Enumeration<NetworkInterface> interfaces = NetworkInterface.getNetworkInterfaces()
  while (interfaces.hasMoreElements()) {
    Enumeration<InetAddress> addresses = interfaces.nextElement().getInetAddresses()
    while (addresses.hasMoreElements()) {
      InetAddress address = addresses.nextElement()
      if (!address.isLoopbackAddress()) {
        if (address.isSiteLocalAddress()) {
          return address.getHostAddress()
        } else if (result == null) {
          result = address
        }
      }
    }
  }
  return (result != null ? result : InetAddress.getLocalHost()).getHostAddress()
}
